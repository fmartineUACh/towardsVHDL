--------------------------------------------------------------------
--
--	muxlut Parameterized Megafunction
--	(subdesign of LPM_MUX)
--
--	Copyright (C) 1991-1997 Altera Corporation
--	Any megafunction design, and related net list (encrypted or decrypted),
--	support information, device programming or simulation file, and any other
--	associated documentation or information provided by Altera or a partner
--	under Altera's Megafunction Partnership Program may be used only to
--	program PLD devices (but not masked PLD devices) from Altera.  Any other
--	use of such megafunction design, net list, support information, device
--	programming or simulation file, or any other related documentation or
--	information is prohibited for any other purpose, including, but not
--	limited to modification, reverse engineering, de-compiling, or use with
--	any other silicon devices, unless such use is explicitly licensed under
--	a separate agreement with Altera or a megafunction partner.  Title to
--	the intellectual property, including patents, copyrights, trademarks,
--	trade secrets, or maskworks, embodied in any such megafunction design,
--	net list, support information, device programming or simulation file, or
--	any other related documentation or information provided by Altera or a
--	megafunction partner, remains with Altera, the megafunction partner, or
--	their respective licensors.  No other licenses, including any licenses
--	needed under any third party's intellectual property, are provided herein.
--
--	Version 3.0
--
--------------------------------------------------------------------
-------------------------------------
-- recursive subdesign for LPM_MUX --
------------------------------------- 
INCLUDE "muxlut.inc";
INCLUDE "altshift.inc";

PARAMETERS
(
	SIZE,				
	LEVEL,
	TOT_LEVELS,
	LATENCY,
	REM_LATENCY = LATENCY,	-- The latency required for single input cases
	LOCAL = 0,
	CASCADE_CHAIN
);

-- Determine the number of select inputs to the current recursion level.
CONSTANT WIDTHS 		  = (SIZE == 1) ? 1 : CEIL(LOG2(SIZE));

-- Determine the number of (levels) stages of the tree, considering the current
-- level as the root of the tree.
CONSTANT STAGES 		  = (SIZE <= 4) ? 1 : CEIL(CEIL(LOG2(SIZE)) DIV 2);
CONSTANT MAX_OUTPUTS 	  = 4^LEVEL;

-- The number of leaves of the tree with the current stage as its root.
CONSTANT STAGE_INPUTS 	  = 4^(STAGES-1);

-- The number of input nodes coming into the current level.
CONSTANT MUX_OUTPUTS_USED = CEIL(SIZE DIV STAGE_INPUTS);

-- The number of leaves of each of lowest branches of the tree with the 
-- current level as its root.
DEFINE SIZE_USED_FOR_BLOCK(b) = ((b + 1)*STAGE_INPUTS) <= SIZE ? STAGE_INPUTS :
							    ((b* STAGE_INPUTS) < SIZE ? SIZE MOD STAGE_INPUTS : 0);

-- Latency-related functions and parameters
DEFINE MOD_DIST(n, d)	 = ((2 * n) > d ? d - n : n);
DEFINE LATENCY_MOD(k)	 = (!((k * (LATENCY + 1)) MOD TOT_LEVELS)) ? TOT_LEVELS : 
							((k * (LATENCY + 1)) MOD TOT_LEVELS);
CONSTANT NEED_CLK		 = -((LEVEL == (TOT_LEVELS-1)) ? 0 :
							((LATENCY_MOD(LEVEL+1) == TOT_LEVELS) # 
						    ((LATENCY_MOD(LEVEL+1) > LATENCY_MOD(LEVEL+2))))); 


SUBDESIGN muxlut
(
	data[SIZE-1..0]			: INPUT;
	select[WIDTHS-1..0]		: INPUT = GND;
	clock, aclr				: INPUT = GND;
	result					: OUTPUT;
	clock_out, aclr_out		: OUTPUT;
)


VARIABLE

	IF !((LOCAL == 0) & (LEVEL > 0 & SIZE <= MAX_OUTPUTS) # SIZE <= 4) GENERATE
		mux_outputs[MUX_OUTPUTS_USED-1..0]	: NODE;
	END GENERATE;

	result_node : NODE;

BEGIN

 	IF (LOCAL == 0) & (LEVEL > 0 & SIZE <= MAX_OUTPUTS) GENERATE
 		IF USED(clock) & LATENCY > 0 GENERATE
			IF NEED_CLK == 1 GENERATE
				result_node = DFF(muxlut(.data[] = data[], 
									.select[] = select[], 
									.clock = clock, .aclr = aclr) 
									WITH (SIZE = SIZE,
									  	  LEVEL = LEVEL-1, TOT_LEVELS = TOT_LEVELS,
										  LATENCY = LATENCY, REM_LATENCY = REM_LATENCY-1, LOCAL = 0)
									RETURNS (.result), clock, !aclr,);

			ELSE GENERATE
				result_node = muxlut(.data[] = data[], 
									.select[] = select[], 
									.clock = clock, .aclr = aclr) 
									WITH (SIZE = SIZE,
									  	  LEVEL = LEVEL-1, TOT_LEVELS = TOT_LEVELS,
										  LATENCY = LATENCY, REM_LATENCY = REM_LATENCY, LOCAL = 0)
									RETURNS (.result);
			END GENERATE;
		ELSE GENERATE
 				result_node = muxlut(.data[] = data[], 
										.select[] = select[]) 
										WITH (SIZE = SIZE,
									 	  LEVEL = LEVEL-1, TOT_LEVELS = TOT_LEVELS,
										  LATENCY = LATENCY, LOCAL = 0)
										RETURNS (.result);
		END GENERATE;

	ELSE GENERATE
	 	IF SIZE == 2 GENERATE
			IF USED(clock) & (NEED_CLK == 1) GENERATE
				result_node = DFF((!select[0] & data[0] # select[0] & data[1]), clock, !aclr,);
			ELSE GENERATE
				result_node = !select[0] & data[0] # select[0] & data[1];
			END GENERATE;
		ELSE GENERATE
			IF SIZE == 3 GENERATE
				IF USED(clock) & (NEED_CLK == 1) GENERATE
					IF CASCADE_CHAIN != "IGNORE" GENERATE
						result_node = !DFF
								(
								(!select[1] # select[0] # !data[2]) &
								CASCADE(!data[0] & !data[1] # !data[0] & !select[0] 
															# !data[1] &  select[0] 
															#  select[1]), clock, !aclr,
								);
					ELSE GENERATE
						result_node = !DFF
								(
								(!select[1] # select[0] # !data[2]) &
									   (!data[0] & !data[1] # !data[0] & !select[0] 
															# !data[1] &  select[0] 
															#  select[1]), clock, !aclr,
								);
					END GENERATE;
		
				ELSE GENERATE
					IF CASCADE_CHAIN != "IGNORE" GENERATE
						result_node = !LCELL
								(
								(!select[1] # select[0] # !data[2]) &
								CASCADE(!data[0] & !data[1] # !data[0] & !select[0] 
															# !data[1] &  select[0] 
															#  select[1])
								);
					ELSE GENERATE
						result_node = !
								(
								(!select[1] # select[0] # !data[2]) &
									   (!data[0] & !data[1] # !data[0] & !select[0] 
															# !data[1] &  select[0] 
															#  select[1])
								);
					END GENERATE;
				END GENERATE;
			ELSE GENERATE
				IF SIZE == 4 GENERATE
					IF USED(clock) & (NEED_CLK == 1) GENERATE
						IF CASCADE_CHAIN != "IGNORE" GENERATE
							result_node = !DFF
									(
									(!select[1] # !data[3] &  select[0] 
												# !data[2] & !data[3] 
												# !data[2] & !select[0]) & CASCADE(!data[0] & !data[1] 
												# !data[0] & !select[0] 
												# !data[1] &  select[0] 
												#  select[1]), clock, !aclr,
									);
						ELSE GENERATE
							result_node = !DFF
									(
									(!select[1] # !data[3] &  select[0] 
												# !data[2] & !data[3] 
												# !data[2] & !select[0]) & (!data[0] & !data[1] 
												# !data[0] & !select[0] 
												# !data[1] &  select[0] 
												#  select[1]), clock, !aclr,
									);
						END GENERATE;
	
					ELSE GENERATE
						IF CASCADE_CHAIN != "IGNORE" GENERATE
							result_node = !LCELL
									(
									(!select[1] # !data[3] &  select[0] 
												# !data[2] & !data[3] 
												# !data[2] & !select[0]) & CASCADE(!data[0] & !data[1] 
												# !data[0] & !select[0] 
												# !data[1] &  select[0] 
												#  select[1])
									);
						ELSE GENERATE
							result_node = !
									(
									(!select[1] # !data[3] &  select[0] 
												# !data[2] & !data[3] 
												# !data[2] & !select[0]) & (!data[0] & !data[1] 
												# !data[0] & !select[0] 
												# !data[1] &  select[0] 
												#  select[1])
									);
						END GENERATE;
					END GENERATE;
	
	 			ELSE GENERATE
					FOR I IN 0 TO MUX_OUTPUTS_USED-1 GENERATE
						IF SIZE_USED_FOR_BLOCK(I) == 1 GENERATE
							-- When no pipelining, we can move the singled-out data input
							-- from the first stage and bring it up to the current level
							-- without any change in functionality of MUX. However, when 
							-- pipelined, this can't be done as the pipe synchrony should be
							-- maintained among all inputs. To do so, a separate pipe with
							-- appropriate latency is inserted between the singled-out data
							-- input and the current stage. Recursion will end here then.
							IF USED(clock) & LATENCY > 0 GENERATE			
								mux_outputs[I] = altshift(data[I*STAGE_INPUTS], clock, aclr)
														WITH (WIDTH = 1, DEPTH = REM_LATENCY - NEED_CLK);
								
							ELSE GENERATE
								mux_outputs[I] = data[I*STAGE_INPUTS];
							END GENERATE;
	
						ELSE GENERATE
							-- Generate the rest of MUX tree
	 						IF USED(clock) & LATENCY > 0 GENERATE
								mux_outputs[I] = muxlut(.data[] = data[(I*STAGE_INPUTS-1 + SIZE_USED_FOR_BLOCK(I))..(I*STAGE_INPUTS)], 
														.select[] = select[CEIL(LOG2(SIZE_USED_FOR_BLOCK(I)))-1..0], 
														.clock = clock, .aclr = aclr) 
														WITH (SIZE = SIZE_USED_FOR_BLOCK(I),
														  	  LEVEL = LEVEL-1, TOT_LEVELS = TOT_LEVELS,
															  LATENCY = LATENCY, 
															  REM_LATENCY = (REM_LATENCY - NEED_CLK),
															  LOCAL = 0)
														RETURNS (.result);
							ELSE GENERATE
	 							mux_outputs[I] = muxlut(.data[] = data[(I*STAGE_INPUTS-1 + SIZE_USED_FOR_BLOCK(I))..(I*STAGE_INPUTS)], 
														.select[] = select[CEIL(LOG2(SIZE_USED_FOR_BLOCK(I)))-1..0]) 
														WITH (SIZE = SIZE_USED_FOR_BLOCK(I),
														 	  LEVEL = LEVEL-1, TOT_LEVELS = TOT_LEVELS,
															  LATENCY = LATENCY, LOCAL = 0)
														RETURNS (.result);
							END GENERATE;
						END GENERATE;
					END GENERATE;
	
					-- This is to simplfy the code and could be done in another, albeit lengthier, way. By 
					-- construction, it is guaraneed that the number of data inputs to the current stage is
					-- less than or equal to 4. Therefore, a recursive call to itself without a change of level
					-- will take us to the special 2, 3, and 4-input cases above and generate the current cell.
					IF USED(clock) & LATENCY > 0 GENERATE	
						result_node = muxlut(.data[] = mux_outputs[], 
											 .select[] = select[WIDTHS-1..WIDTHS-CEIL(LOG2(MUX_OUTPUTS_USED))], 
											 .clock = clock, .aclr = aclr)  
											 WITH (SIZE = MUX_OUTPUTS_USED,
												   LEVEL = LEVEL, TOT_LEVELS = TOT_LEVELS,
												   LATENCY = LATENCY, LOCAL = 1)
											 RETURNS (.result);
					ELSE GENERATE
						result_node = muxlut(.data[] = mux_outputs[], 
											 .select[] = select[WIDTHS-1..WIDTHS-CEIL(LOG2(MUX_OUTPUTS_USED))])  
											 WITH (SIZE = MUX_OUTPUTS_USED,
												   LEVEL = LEVEL, TOT_LEVELS = TOT_LEVELS,
												   LATENCY = LATENCY, LOCAL = 1)
											 RETURNS (.result);
					END GENERATE;
				END GENERATE;
			END GENERATE;
		END GENERATE;
	END GENERATE;

	-- Clock connections are made to the current level cell.
 	IF USED(clock) & (NEED_CLK == 1) GENERATE
		ASSERT REPORT "stage % needs clock" LEVEL SEVERITY DEBUG;
	END GENERATE;

	result = result_node;
	clock_out = clock;
	aclr_out = aclr;
END;
 
